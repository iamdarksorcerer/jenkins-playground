buildscript {
	ext {
		springBootVersion = '1.5.6.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.ajoberstar:grgit:1.1.0")
	}
}

import org.ajoberstar.grgit.Grgit

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

ext {
    git = Grgit.open(project.file('../.'))
    timeZone = TimeZone.getTimeZone('GMT')
    timeFormat = 'yyyy/MM/dd HH:mm:ss z'

    buildTime = new Date().format(timeFormat, timeZone)
    commitId = git.head().id
    commitTime = git.head().date.format(timeFormat, timeZone)
    dirty = !git.status().clean ? "yes" : "no"
}


version = '1.0.0-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.boot:spring-boot-starter-data-redis')
	compile('org.springframework.boot:spring-boot-starter-web')

	runtime('org.springframework.boot:spring-boot-devtools')

    compileOnly('org.projectlombok:lombok')

	testCompile('org.springframework.boot:spring-boot-starter-test')
}

processResources {
    from("src/main/templates")
    expand(project.properties)
}